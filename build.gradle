//file:noinspection SpellCheckingInspection

plugins {
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
    id "org.sonarqube" version "3.0"
}

apply from: "build-ext.gradle"

group = 'portaldev'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'
targetCompatibility = '16'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    testApiRestImplementation.extendsFrom testImplementation
    testApiRestRuntime.extendsFrom testRuntime

    testSpiPostgresImplementation.extendsFrom testImplementation
    testSpiPostgresRuntime.extendsFrom testRuntime
}

sourceSets {
    testApiRest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    testSpiPostgres {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}
idea {
    module {
        testSourceDirs += project.sourceSets.testApiRest.java.srcDirs
        testSourceDirs += project.sourceSets.testApiRest.resources.srcDirs

        testSourceDirs += project.sourceSets.testSpiPostgres.java.srcDirs
        testSourceDirs += project.sourceSets.testSpiPostgres.resources.srcDirs
    }
}

apply from: "build-dependencies.gradle"
apply from: "build-tasks.gradle"